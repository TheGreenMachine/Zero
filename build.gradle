import edu.wpi.first.gradlerio.deploy.roborio.RoboRIO
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2023.2.1"
    id 'org.jsonschema2dataclass' version '4.0.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def ROBOT_MAIN_CLASS = "com.team1816.season.Main"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project DeployUtils.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = 1816
            debug = project.frc.getDebugOrDefault(false)
            checkImage = false
            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                }
            }
        }
    }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for JNI.
wpi.java.debugJni = false

// Set this to true to enable desktop support.
def includeDesktopSupport = true

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://www.jitpack.io" }
}

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 4.
dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    implementation "com.github.dominikwin:badlog:v0.1.1"
    implementation 'org.yaml:snakeyaml:1.30'
    implementation 'gov.nist.math:jama:1.0.3'
    implementation 'com.google.inject:guice:5.1.0'
    implementation 'com.google.inject.extensions:guice-assistedinject:5.1.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.7'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-inline:4.7.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

// Simulation configuration (e.g. environment variables).
wpi.sim.addGui().defaultEnabled.set(true)
wpi.sim.addDriverstation()

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Converts JSON Schema to Classes
jsonSchema2Pojo {
    targetPackage = 'com.team1816.lib'
    useTitleAsClassname = true
    includeGetters = false
    includeSetters = false
    removeOldOutput = true
    includeAdditionalProperties = false
    includeToString = true
    includeHashcodeAndEquals = false
    source.setFrom files(
        "${project.rootDir}/src/main/resources/schemas"
    )
}

// Calculates Trajectories
apply plugin: 'java';
task calculateTrajectories(type: JavaExec) {
    String jniDir = "${project.buildDir}/jni/release"
    environment "PATH", jniDir
    String hal = fileTree(dir: "${project.buildDir}/jni/release", include: '*halsim_*.*').asPath
    if (!DefaultNativePlatform.currentOperatingSystem.isWindows()) {
        environment "LD_LIBRARY_PATH", jniDir
        environment "DYLD_FALLBACK_LIBRARY_PATH", jniDir
        environment "DYLD_LIBRARY_PATH", jniDir
        jvmArgs "-XstartOnFirstThread"
    }
    environment "HALSIM_EXTENSIONS", hal
    environment "ROBOT_NAME", "alpha"
    classpath = files(jar)
    jvmArgs "-Djava.library.path=${jniDir}"
    main = "com.team1816.lib.motion.TrajectoryCalculator"
}

// Simulates robot with characterization alpha.config.yml
task runAlpha(type: JavaExec) {
    calculateTrajectories
    String jniDir = "${project.buildDir}/jni/release"
    environment "PATH", jniDir
    String hal = fileTree(dir: "${project.buildDir}/jni/release", include: '*halsim_*.*').asPath
    if (!DefaultNativePlatform.currentOperatingSystem.isWindows()) {
        environment "LD_LIBRARY_PATH", jniDir
        environment "DYLD_FALLBACK_LIBRARY_PATH", jniDir
        environment "DYLD_LIBRARY_PATH", jniDir
        jvmArgs "-XstartOnFirstThread"
    }
    environment "HALSIM_EXTENSIONS", hal
    environment "ROBOT_NAME", "alpha"
    dependsOn(jar, simulateExternalJavaRelease)
    classpath = files(jar)
    jvmArgs "-Djava.library.path=${jniDir}"
}

// Simulates robot with characterization zero.config.yml
task runZero(type: JavaExec) {
    calculateTrajectories
    String jniDir = "${project.buildDir}/jni/release"
    environment "PATH", jniDir
    String hal = fileTree(dir: "${project.buildDir}/jni/release", include: '*halsim_*.*').asPath
    if (!DefaultNativePlatform.currentOperatingSystem.isWindows()) {
        environment "LD_LIBRARY_PATH", jniDir
        environment "DYLD_FALLBACK_LIBRARY_PATH", jniDir
        environment "DYLD_LIBRARY_PATH", jniDir
        jvmArgs "-XstartOnFirstThread"
    }
    environment "HALSIM_EXTENSIONS", hal
    environment "ROBOT_NAME", "zero"
    dependsOn(jar, simulateExternalJavaRelease)
    classpath = files(jar)
    jvmArgs "-Djava.library.path=${jniDir}"
}

// Simulates robot with characterization zoffseason.config.yml
task runZoffseason(type: JavaExec) {
    calculateTrajectories
    String jniDir = "${project.buildDir}/jni/release"
    environment "PATH", jniDir
    String hal = fileTree(dir: "${project.buildDir}/jni/release", include: '*halsim_*.*').asPath
    if (!DefaultNativePlatform.currentOperatingSystem.isWindows()) {
        environment "LD_LIBRARY_PATH", jniDir
        environment "DYLD_FALLBACK_LIBRARY_PATH", jniDir
        environment "DYLD_LIBRARY_PATH", jniDir
        jvmArgs "-XstartOnFirstThread"
    }
    environment "HALSIM_EXTENSIONS", hal
    environment "ROBOT_NAME", "zoffseason"
    dependsOn(jar, simulateExternalJavaRelease)
    classpath = files(jar)
    jvmArgs "-Djava.library.path=${jniDir}"
}

// Simulates robot with characterization zodiac_pro.config.yml
task runZodiac(type: JavaExec) {
    calculateTrajectories
    String jniDir = "${project.buildDir}/jni/release"
    environment "PATH", jniDir
    String hal = fileTree(dir: "${project.buildDir}/jni/release", include: '*halsim_*.*').asPath
    if (!DefaultNativePlatform.currentOperatingSystem.isWindows()) {
        environment "LD_LIBRARY_PATH", jniDir
        environment "DYLD_FALLBACK_LIBRARY_PATH", jniDir
        environment "DYLD_LIBRARY_PATH", jniDir
        jvmArgs "-XstartOnFirstThread"
    }
    environment "HALSIM_EXTENSIONS", hal
    environment "ROBOT_NAME", "zodiac_pro"
    dependsOn(jar, simulateExternalJavaRelease)
    classpath = files(jar)
    jvmArgs "-Djava.library.path=${jniDir}"
}

processResources {
    include '**/*.config.yml'
    eachFile { fileCopyDetails ->
    }
}

wrapper {
    gradleVersion = '7.2'
}

// Configure jar and deploy tasks
deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

compileJava {
    //dependsOn tasks.named("spotlessApply")
}

task fullDeploy() {
    dependsOn tasks.named("calculateTrajectories")
    dependsOn 'deploy'
}